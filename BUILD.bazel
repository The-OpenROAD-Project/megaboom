load("@bazel-orfs//:openroad.bzl", "orfs_flow", "orfs_run")
load("@bazel-orfs//:sweep.bzl", "orfs_sweep")
load(":snapshots/kept.bzl", "KEPT_MODULES")
load(":write_binary.bzl", "write_binary")

SRAM_ABSTRACT_STAGE = "cts"

filegroup(
    name = "io-sram",
    srcs = [
        "io-sram.tcl",
    ],
    data = [
        "util.tcl",
    ],
    visibility = [":__subpackages__"],
)

filegroup(
    name = "io-sram-bottom",
    srcs = [
        "io-sram-bottom.tcl",
    ],
    data = [
        "util.tcl",
    ],
    visibility = [":__subpackages__"],
)

filegroup(
    name = "io-boomtile",
    srcs = [
        "io-boomtile.tcl",
    ],
    data = [
        "util.tcl",
    ],
)

filegroup(
    name = "io",
    srcs = [
        "io.tcl",
    ],
    data = [
        "util.tcl",
    ],
)

filegroup(
    name = "io-top",
    srcs = [
        "io-top.tcl",
    ],
    data = [
        "util.tcl",
    ],
)

filegroup(
    name = "constraints-sram",
    srcs = [
        "constraints.sdc",
    ],
    visibility = [":__subpackages__"],
)

filegroup(
    name = "constraints-boomtile",
    srcs = [
        "constraints-boomtile.sdc",
    ],
    visibility = [":__subpackages__"],
)

SHARED_SETTINGS = {
    # repair_timing runs for hours in floorplan
    "TNS_END_PERCENT": "0",
    "SKIP_CTS_REPAIR_TIMING": "1",
    "GPL_TIMING_DRIVEN": "0",
    "GPL_ROUTABILITY_DRIVEN": "1",
    # Save global route time for now
    "SKIP_INCREMENTAL_REPAIR": "1",
    "FILL_CELLS": "",
    "TAPCELL_TCL": "",
    # faster synthesis
    "SYNTH_MOCK_LARGE_MEMORIES": "1",
    # Stress test slang
    "SYNTH_HDL_FRONTEND": "slang",
    # smoketest
    #"SWAP_ARITH_OPERATORS": "1",
    "OPENROAD_HIERARCHICAL": "1",
}

BLOCK_FLOORPLAN = {
    "PDN_TCL": "$(PLATFORM_DIR)/openRoad/pdn/BLOCK_grid_strategy.tcl",
}

SRAM_SYNTH_ARGUMENTS = {"SDC_FILE": "$(location :constraints-sram)"} | SHARED_SETTINGS

SRAM_FLOOR_PLACE_ARGUMENTS = {"IO_CONSTRAINTS": "$(location :io-sram)"}

SKIP_REPORT_METRICS = {"SKIP_REPORT_METRICS": "1"}

mock_files = [
    "mock/array_256x128.sv",
    "mock/data_2048x8.sv",
    "mock/dataArrayB_256x64.sv",
    "mock/ebtb_128x40.sv",
    "mock/l2_tlb_ram_0_512x46.sv",
    "mock/regfile_128x64.sv",
    "mock/regfile_128x65.sv",
    "mock/tag_array_64x184.sv",
]

all_source_files = sorted(glob(
    include = [
        "rtl/*.sv",
        "rtl/*.v",
    ],
    exclude = ([f.replace("mock/", "rtl/") for f in mock_files] + [
        "rtl/ClockSourceAtFreqMHz.v",
        "rtl/SimJTAG.v",
        "rtl/SimTSI.v",
    ]),
) + mock_files)

# Haven't tried mocking any SRAMs with mock_area of None. Rest have been tried
# with experiment notes on the same line
boom_tile_small_srams = {
    "tag_array_64x184": {
        "mock_area": 0.62,
        "aspect_ratio": "0.33",
    },
    "tag_array_64x168": {
        "mock_area": 0.31,
        "aspect_ratio": "0.36",
    },
    # "data_2048x2": { "mock_area": None, "aspect_ratio": "2" },
    "table_256x48": {
        "mock_area": 0.31,
        "aspect_ratio": "1.25",
    },  #close
    "table_128x52": {
        "mock_area": 0.31,
        "aspect_ratio": "0.58",
    },  #close
    "table_128x44": {
        "mock_area": 0.31,
        "aspect_ratio": "0.68",
    },  # close
    "btb_128x56": {
        "mock_area": 0.309,
        "aspect_ratio": "0.53",
    },  # close
    "meta_128x120": {
        "mock_area": 0.31,
        "aspect_ratio": "0.25",
    },
    "lb_32x128": {
        "mock_area": 0.32,
        "aspect_ratio": "0.93",
    },
    "sdq_17x64": {
        "mock_area": 0.32,
        "aspect_ratio": "1.2",
    },
    "data_2048x8": {
        "mock_area": 3.2,
        # "aspect_ratio": "58",
        "core_utilization": "10",
    },
    "mem_256x4": {
        "mock_area": 0.265,
        # "aspect_ratio": "36",
    },
}

boom_tile_rams = {
    # "l2_tlb_ram_0_512x46": { "mock_area": None, "aspect_ratio": "2" },
    "ebtb_128x40": {
        "mock_area": 0.86,
        "aspect_ratio": "0.75",
    },  # close
    "array_256x128": {
        "mock_area": 1.225,
        "aspect_ratio": "0.47",
    },  # close
    "dataArrayB_256x64": {
        "mock_area": 1.23,
        "aspect_ratio": "0.94",
    },  # close
    # "l2_tlb_ram_0_512x45": { "mock_area": None, "aspect_ratio": "2" },
}

digital_top_srams = {
    # "cc_dir_1024x168": { "mock_area": None, "aspect_ratio": "2" },
    # "data_data_40x128": { "mock_area": None, "aspect_ratio": "2" },
    # "ghist_40x64": { "mock_area": None, "aspect_ratio": "2" },
    "meta_40x240": {
        "mock_area": 0.48,
        "aspect_ratio": "0.64",
    },  # ran out of left edge pin slots - can get closer by adjusting aspect ratio but only to within 1000 um^2
}

# combine SRAMs into one dictionary for processing
all_srams = boom_tile_rams | boom_tile_small_srams | digital_top_srams

write_binary(
    name = "srams",
    data = str(all_srams),
)

[
    orfs_flow(
        name = ram,
        abstract_stage = SRAM_ABSTRACT_STAGE,
        arguments = SRAM_SYNTH_ARGUMENTS | SKIP_REPORT_METRICS | {"SYNTH_MEMORY_MAX_BITS": "16384"} | {
                        "PLACE_PINS_ARGS": "-min_distance 1 -min_distance_in_tracks",
                    } | BLOCK_FLOORPLAN |
                    {
                        "CORE_UTILIZATION": ram_data.get("core_utilization", "40"),
                        "CORE_ASPECT_RATIO": ram_data.get("aspect_ratio", "1"),
                    } | SRAM_FLOOR_PLACE_ARGUMENTS | {
            "PLACE_DENSITY": "0.65",
            "GPL_TIMING_DRIVEN": "0",
        } | {
            "SKIP_CTS_REPAIR_TIMING": "0",
            "SKIP_LAST_GASP": "1",
            "SETUP_SLACK_MARGIN": "-500",
        },
        mock_area = ram_data["mock_area"],
        stage_sources = {
            "synth": [":constraints-sram"],
            "floorplan": [":io-sram"],
            "place": [":io-sram"],
        },
        verilog_files = [
            {
                "array_256x128": "mock/",
                "cc_dir_1024x168": "mock/",
                "data_2048x2": "mock/",
                "dataArrayB_256x64": "mock/",
                "data_data_40x128": "mock/",
                "ebtb_128x40": "mock/",
                "ghist_40x64": "mock/",
                "l2_tlb_ram_0_512x45": "mock/",
                "tag_array_64x184": "mock/",
                "data_2048x8": "mock/",
            }.get(ram, "rtl/") + ram + ".sv",
        ],
    )
    for ram, ram_data in all_srams.items()
]

all_srams_stage_num = {
    "floorplan": "2",
    "place": "3",
    "cts": "4",
    "grt": "5_1",
    "route": "5_2",
}[SRAM_ABSTRACT_STAGE]

[orfs_run(
    name = ram + "_timing_report",
    src = ":" + ram + "_" + SRAM_ABSTRACT_STAGE,
    outs = [
        ram + ".yaml",
    ],
    arguments = {
        "OUTFILE": "$(location :" + ram + ".yaml)",
        "STAGE": all_srams_stage_num + "_" + SRAM_ABSTRACT_STAGE,
    },
    script = ":report-sram.tcl",
) for ram in all_srams.keys()]

[orfs_run(
    name = ram + "_floorplan_report",
    src = ":" + ram + "_mocked_floorplan",
    outs = [
        ram + "_floorplan.yaml",
    ],
    arguments = {"OUTFILE": "$(location :" + ram + "_floorplan.yaml)"},
    script = ":report-floorplan.tcl",
) for ram in all_srams.keys()]

genrule(
    name = "sram_report",
    srcs = ["sram.py"] + [
               ":srams",
           ] + [":" + ram + "_timing_report" for ram in all_srams.keys()] +
           [":" + ram + "_floorplan_report" for ram in all_srams.keys()],
    outs = ["sram-report.md"],
    cmd = (
        "$(location :sram.py) $(location :srams) " +
        " ".join(["$(location :" + ram + "_timing_report)" for ram in all_srams.keys()] +
                 ["$(location :" + ram + "_floorplan_report)" for ram in all_srams.keys()]) +
        " > $@"
    ),
    visibility = ["//visibility:public"],
)

boom_regfile_rams = {
    "regfile_128x64": ":io-sram-bottom",
    "regfile_128x65": ":io-sram",
}

[
    orfs_flow(
        name = ram,
        abstract_stage = SRAM_ABSTRACT_STAGE,
        arguments = BLOCK_FLOORPLAN | SHARED_SETTINGS | {
            "DIE_AREA": "0 0 400 400",
            "CORE_AREA": "2 2 398 398",
        } | {
            "PLACE_DENSITY": {
                "regfile_128x64": "0.42",
                "regfile_128x65": "0.3",
            }.get(ram),
        },
        mock_area = 0.5,
        sources = {
            "IO_CONSTRAINTS": [io_constraint],
            "SDC_FILE": [":constraints-sram"],
        },
        verilog_files = ["mock/" + ram + ".sv"],
    )
    for ram, io_constraint in boom_regfile_rams.items()
]

orfs_run(
    name = "xy_stats",
    src = ":BoomTile_cts",
    outs = ["xy_stats.txt"],
    arguments = {
        "OUTPUT": "$(location :xy_stats.txt)",
    },
    script = ":slack-positions.tcl",
)

sh_binary(
    name = "show_skew",
    srcs = ["plot-3d-slack.py"],
    args = ["$(location :xy_stats)"],
    data = ["xy_stats"],
    visibility = ["//visibility:public"],
)

orfs_run(
    name = "kpi",
    src = ":BoomTile_cts",
    outs = ["kpi.yaml"],
    arguments = {
        "OUTPUT": "$(location :kpi.yaml)",
    },
    script = ":report-kpi.tcl",
)

OTHER_VARIANTS = {
    "macro": {
        "description": "Base settings, provides macro placement from hierarchical synthesis",
        "sources": {"SYNTH_NETLIST_FILES": [":netlists"]},
        "arguments": {
        },
    },
    "hierarchical": {
        "description": "Base settings, hierarchical synthesis",
    },
    "naja": {
        "renamed_inputs": {
            "floorplan": {"1_synth.v": ":naja"},
        },
        "previous_stage": {
            "floorplan": "BoomTile_synth",
        },
    },
}

BOOMTILE_VARIABLES = SKIP_REPORT_METRICS | SHARED_SETTINGS | {
    "PDN_TCL": "$(PLATFORM_DIR)/openRoad/pdn/BLOCKS_grid_strategy.tcl",
    "IO_CONSTRAINTS": "$(location :io-boomtile)",
    "PLACE_DENSITY": "0.40",
    # We only need hierarchical synthesis when we are running through floorplan
    # write_macro_placement macros.tcl
    "SYNTH_HIERARCHICAL": "1",
    "SDC_FILE": "$(location :constraints-boomtile)",
    "MACRO_PLACE_HALO": "5 5",
    "PLACE_PINS_ARGS": "-annealing",
    "MIN_ROUTING_LAYER": "M2",
    "MAX_ROUTING_LAYER": "M7",
    # "ROUTING_LAYER_ADJUSTMENT": "0.45",
    "DIE_AREA": "0 0 1000 1000",
    "CORE_AREA": "2 2 998 998",
    # Saves hours of build time, specific to BoomTile
    "SKIP_LAST_GASP": "1",
    "SETUP_SLACK_MARGIN": "-2500",
    "HOLD_SLACK_MARGIN": "-400",
    "OPENROAD_HIERARCHICAL": "1",
}

BOOMTILE_SOURCES = {
    "synth": [":constraints-boomtile"],
    "floorplan": [
        ":io-boomtile",
    ],
    "place": [":io-boomtile"],
}

boomtile_all_rams = boom_regfile_rams.keys() + all_srams.keys()

BRANCH_PREDICTOR_VARIABLES = SKIP_REPORT_METRICS | SHARED_SETTINGS | {
    "PDN_TCL": "$(PLATFORM_DIR)/openRoad/pdn/BLOCKS_grid_strategy.tcl",
    "IO_CONSTRAINTS": "$(location :io-boomtile)",
    "PLACE_DENSITY": "0.24",
    # We only need hierarchical synthesis when we are running through floorplan
    # write_macro_placement macros.tcl
    "SYNTH_HIERARCHICAL": "1",
    "SDC_FILE": "$(location :constraints-boomtile)",
    "MACRO_PLACE_HALO": "4 4",
    "PLACE_PINS_ARGS": "-annealing",
    # Saves hours of build time, specific to BoomTile
    "SKIP_LAST_GASP": "1",
    "SETUP_SLACK_MARGIN": "-1300",
    "HOLD_SLACK_MARGIN": "-200",
}

[orfs_flow(
    name = module,
    abstract_stage = "cts",
    arguments = BRANCH_PREDICTOR_VARIABLES | {
        "ComposedBranchPredictorBank": {
            "DIE_AREA": "0 0 300 400",
            "CORE_AREA": "0 0 298 398",
        },
        "BranchPredictor": {
            "DIE_AREA": "0 0 600 400",
            "CORE_AREA": "0 0 598 398",
        },
    }[module],
    macros = [
        ":" + m + "_generate_abstract"
        for m in boomtile_all_rams
    ],
    stage_sources = BOOMTILE_SOURCES,
    verilog_files = all_source_files,
) for module in [
    "ComposedBranchPredictorBank",
    "BranchPredictor",
]]

orfs_sweep(
    name = "BoomTile",
    arguments = BOOMTILE_VARIABLES,
    macros = [
        ":" + m + "_generate_abstract"
        for m in boomtile_all_rams
    ],
    other_variants = OTHER_VARIANTS,
    stage_sources = BOOMTILE_SOURCES,
    sweep = {
        "base": {
            "description": "Flattend, timing driven placement",
            "arguments": {
                "SYNTH_HIERARCHICAL": "0",
                "SYNTH_RETIME_MODULES": "BoomTile",
                "GPL_TIMING_DRIVEN": "1",
                "SKIP_CTS_REPAIR_TIMING": "0",
                "SKIP_LAST_GASP": "0",
            },
            "sources": {
                "MACRO_PLACEMENT_TCL": [":macro_placement.tcl"],
            },
        },
    },
    verilog_files = all_source_files,
)

# fish out the file that we want as input to the "naja" rule
# below.
filegroup(
    name = "BoomTile_synth_unedited",
    srcs = ["BoomTile_synth"],
    output_group = "1_synth.v",
)

[filegroup(
    name = ram + "_lib",
    srcs = [ram + "_generate_abstract"],
    output_group = ram + "_typ.lib",
) for ram in all_srams.keys() + boom_regfile_rams.keys()]

filegroup(
    name = "BoomTile_synth_mergedlib",
    srcs = ["BoomTile_synth"],
    output_group = "merged.lib",
)

# Read in the renamed synthesis output from above and provide
# the original name to the next stage
genrule(
    name = "naja",
    srcs = [
        ":BoomTile_synth_unedited",
        ":BoomTile_synth_mergedlib",
        "naja_edit/install/bin/naja_edit",
        "naja_edit/install/lib/python",
        "naja_edit/install/share/naja/primitives/asap7_new.py",
    ] + [":" + ram + "_lib" for ram in boomtile_all_rams],
    outs = ["naja.v"],
    #cmd = "mkdir -p `dirname $@`; export PYTHONPATH=$(location naja_edit/install/lib/python) && $(location naja_edit/install/bin/naja_edit) -a all -p $(location :BoomTile_synth_mergedlib) -f verilog -t verilog -i $(location :BoomTile_synth_unedited) -o $@",
    #cmd = "mkdir -p `dirname $@`; export PYTHONPATH=$(location naja_edit/install/lib/python) && $(location naja_edit/install/bin/naja_edit) -a all -p $(location naja_edit/install/share/naja/primitives/asap7_new.py) -f verilog -t verilog -i $(location :BoomTile_synth_unedited) -o $@",
    cmd = ("mkdir -p `dirname $@`; export PYTHONPATH=$(location naja_edit/install/lib/python) && $(location naja_edit/install/bin/naja_edit) -a all -p " +
           " ".join(["$(location :" + ram + "_lib)" for ram in boomtile_all_rams]) +
           " $(location :naja_edit/install/share/naja/primitives/asap7_new.py)" +
           " -f verilog -t verilog -i $(location :BoomTile_synth_unedited) -o $@"),
)

# Use the macro placement from a different flow
orfs_run(
    name = "write_macro_placement",
    src = ":BoomTile_macro_floorplan",
    outs = [
        ":macro_placement.tcl",
    ],
    script = ":write_macros.tcl",
)

orfs_run(
    name = "write_kept",
    src = ":BoomTile_hierarchical_synth",
    outs = [
        ":kept.bzl",
    ],
    cmd = """ \
    run-yosys \"RUN_YOSYS_ARGS=-c $(location :write_kept.tcl)\"""",
    data = [
        ":write_kept.tcl",
    ],
    script = ":write_kept.tcl",
)

sh_binary(
    name = "update",
    srcs = ["update.sh"],
    args = [
        "$(location :kept.bzl)",
        "$(location :macro_placement.tcl)",
    ],
    data = [
        ":kept.bzl",
        ":macro_placement.tcl",
    ],
)

[orfs_flow(
    name = "{name}".format(name = name),
    arguments = BOOMTILE_VARIABLES | {
        "SYNTH_BLACKBOXES": " ".join([b for b in KEPT_MODULES if b != name]),
        "SYNTH_HIERARCHICAL": "0",
    },
    macros = [
        ":" + m + "_generate_abstract"
        for m in boomtile_all_rams
    ],
    stage_sources = BOOMTILE_SOURCES,
    variant = "netlist",
    verilog_files = all_source_files,
) for name in KEPT_MODULES]

[filegroup(
    name = "{name}_netlist".format(name = name),
    srcs = ["{name}_netlist_synth".format(name = name)],
    output_group = "1_synth.v",
) for name in KEPT_MODULES]

# ca. 540 seconds for sequential synthesis, vs 90 seconds for netlist synthesis.
filegroup(
    name = "netlists",
    srcs = [":{}_netlist".format(name) for name in KEPT_MODULES],
)

py_binary(
    name = "print_python_version",
    srcs = ["print_python_version.py"],
)
